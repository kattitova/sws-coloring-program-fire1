; Script generated by the HM NIS Edit Script Wizard.

!define PRODUCT_NAME "sendmail"
!define PRODUCT_VERSION "26"
!define PRODUCT_PUBLISHER "glob"
!define PRODUCT_WEB_SITE "http://www.glob.com.au/sendmail/"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\sendmail.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

!include "MUI.nsh"

!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE "..\license.txt"
Page custom Bugzilla BugzillaValidate
Page custom Config ConfigValidate
Page custom Auth AuthValidate
!insertmacro MUI_PAGE_INSTFILES
!insertmacro MUI_PAGE_FINISH
!INSERTMACRO MUI_UNPAGE_INSTFILES
!insertmacro MUI_LANGUAGE "English"

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "..\..\sendmail-bugzilla-setup.exe"
InstallDir "\usr\lib"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show
BrandingText " "

Var HWND
Var SMTP_SERVER
Var DOMAIN
Var USERNAME
Var PASSWORD
Var POP3_SERVER

Function .onInit
  !insertmacro MUI_INSTALLOPTIONS_EXTRACT "bugzilla.ini"
  !insertmacro MUI_INSTALLOPTIONS_EXTRACT "config.ini"
  !insertmacro MUI_INSTALLOPTIONS_EXTRACT "auth.ini"
FunctionEnd

Section "MainSection" SEC01
  SetOutPath "$INSTDIR"
  SetOverwrite ifnewer
  File "..\sendmail.exe"
  File "..\ReadMe.html"
  File "..\license.txt"
  File "sendmail.dpr"
  File "sendmail.mes"
  SetOverwrite off
  File "..\sendmail.ini"
  WriteIniStr "$INSTDIR\sendmail.ini" "sendmail" "smtp_server" $SMTP_SERVER
  WriteIniStr "$INSTDIR\sendmail.ini" "sendmail" "default_domain" $DOMAIN
  WriteIniStr "$INSTDIR\sendmail.ini" "sendmail" "auth_username" $USERNAME
  WriteIniStr "$INSTDIR\sendmail.ini" "sendmail" "auth_password" $PASSWORD
  WriteIniStr "$INSTDIR\sendmail.ini" "sendmail" "pop3_server" $POP3_SERVER
  WriteIniStr "$INSTDIR\sendmail.ini" "sendmail" "pop3_username" $USERNAME
  WriteIniStr "$INSTDIR\sendmail.ini" "sendmail" "pop3_password" $PASSWORD
SectionEnd

Function Bugzilla
  !insertmacro MUI_HEADER_TEXT "Bugzilla Directory" "Please select Bugzilla installation directory."
  !insertmacro MUI_INSTALLOPTIONS_DISPLAY "bugzilla.ini"
FunctionEnd

Function BugzillaValidate
  ReadINIStr $R0 "$PLUGINSDIR\bugzilla.ini" "Field 2" "State"
  StrCmp $R0 "" 0 +3
    MessageBox MB_ICONEXCLAMATION|MB_OK "You must select the Bugzilla installation directory."
    Abort
  IfFileExists "$R0\checksetup.pl" +3
    MessageBox MB_ICONEXCLAMATION|MB_OK "Failed to find Bugzilla in the selected directory."
    Abort
  StrCpy $1 $R0 1
  StrCpy $INSTDIR "$1:\usr\lib"
FunctionEnd

Function Config
  ReadIniStr $SMTP_SERVER "$INSTDIR\sendmail.ini" "sendmail" "smtp_server"
  ReadIniStr $DOMAIN "$INSTDIR\sendmail.ini" "sendmail" "default_domain"
  WriteIniStr "$PLUGINSDIR\config.ini" "Field 1" "State" $SMTP_SERVER
  WriteIniStr "$PLUGINSDIR\config.ini" "Field 2" "State" $DOMAIN
  !insertmacro MUI_HEADER_TEXT "Sendmail Configuration" "Please configure Sendmail."
  !insertmacro MUI_INSTALLOPTIONS_DISPLAY "config.ini"
FunctionEnd

Function ConfigValidate
  ReadINIStr $SMTP_SERVER "$PLUGINSDIR\config.ini" "Field 1" "State"
  ReadINIStr $DOMAIN "$PLUGINSDIR\config.ini" "Field 2" "State"
  StrCmp $SMTP_SERVER "" 0 +3
    MessageBox MB_ICONEXCLAMATION|MB_OK "You must enter a SMTP Server."
    Abort
  StrCmp $SMTP_SERVER "mail.example.com" 0 +3
    MessageBox MB_ICONEXCLAMATION|MB_OK "You must enter a valid SMTP Server."
    Abort
  StrCmp $DOMAIN "" 0 +3
    MessageBox MB_ICONEXCLAMATION|MB_OK "You must enter your default domain."
    Abort
  StrCmp $DOMAIN "example.com" 0 +3
    MessageBox MB_ICONEXCLAMATION|MB_OK "You must enter a valid default domain."
    Abort
FunctionEnd

Function Auth
  ReadIniStr $USERNAME "$INSTDIR\sendmail.ini" "sendmail" "auth_username"
  ReadIniStr $PASSWORD "$INSTDIR\sendmail.ini" "sendmail" "auth_password"
  StrCmp $USERNAME "" setPop3 setAuth
setAuth:
  WriteIniStr "$PLUGINSDIR\auth.ini" "Field 1" "State" ""
  WriteIniStr "$PLUGINSDIR\auth.ini" "Field 2" "State" "1"
  WriteIniStr "$PLUGINSDIR\auth.ini" "Field 3" "Flags" ""
  WriteIniStr "$PLUGINSDIR\auth.ini" "Field 3" "State" $USERNAME
  WriteIniStr "$PLUGINSDIR\auth.ini" "Field 4" "Flags" ""
  WriteIniStr "$PLUGINSDIR\auth.ini" "Field 4" "State" $PASSWORD
  Goto done
setPop3:
  ReadIniStr $POP3_SERVER "$INSTDIR\sendmail.ini" "sendmail" "pop3_server"
  ReadIniStr $USERNAME "$INSTDIR\sendmail.ini" "sendmail" "pop3_username"
  ReadIniStr $PASSWORD "$INSTDIR\sendmail.ini" "sendmail" "pop3_password"
  StrCmp $USERNAME "" done 0
  WriteIniStr "$PLUGINSDIR\auth.ini" "Field 1" "State" ""
  WriteIniStr "$PLUGINSDIR\auth.ini" "Field 5" "State" "1"
  WriteIniStr "$PLUGINSDIR\auth.ini" "Field 6" "Flags" ""
  WriteIniStr "$PLUGINSDIR\auth.ini" "Field 6" "State" $POP3_SERVER
  WriteIniStr "$PLUGINSDIR\auth.ini" "Field 7" "Flags" ""
  WriteIniStr "$PLUGINSDIR\auth.ini" "Field 7" "State" $USERNAME
  WriteIniStr "$PLUGINSDIR\auth.ini" "Field 8" "Flags" ""
  WriteIniStr "$PLUGINSDIR\auth.ini" "Field 8" "State" $PASSWORD
done:
  !insertmacro MUI_HEADER_TEXT "SMTP AUthenication Configuration" "If your SMTP Server requires authentication, please enter the details."
  InstallOptions::initDialog /NOUNLOAD "$PLUGINSDIR\auth.ini"
  Pop $HWND
  InstallOptions::show
  Pop $0
FunctionEnd

Function AuthValidate
  ReadINIStr $0 "$PLUGINSDIR\auth.ini" "Settings" "State"
  StrCmp $0 0 validate
  StrCmp $0 1 noneBtn
  StrCmp $0 2 userBtn
  StrCmp $0 5 pop3Btn
  Abort
noneBtn:
  GetDlgItem $1 $HWND 1202
  EnableWindow $1 0
  GetDlgItem $1 $HWND 1203
  EnableWindow $1 0
  GetDlgItem $1 $HWND 1205
  EnableWindow $1 0
  GetDlgItem $1 $HWND 1206
  EnableWindow $1 0
  GetDlgItem $1 $HWND 1207
  EnableWindow $1 0
  Abort
userBtn:
  GetDlgItem $1 $HWND 1202
  EnableWindow $1 1
  GetDlgItem $1 $HWND 1203
  EnableWindow $1 1
  GetDlgItem $1 $HWND 1205
  EnableWindow $1 0
  GetDlgItem $1 $HWND 1206
  EnableWindow $1 0
  GetDlgItem $1 $HWND 1207
  EnableWindow $1 0
  Abort
pop3Btn:
  GetDlgItem $1 $HWND 1202
  EnableWindow $1 0
  GetDlgItem $1 $HWND 1203
  EnableWindow $1 0
  GetDlgItem $1 $HWND 1205
  EnableWindow $1 1
  GetDlgItem $1 $HWND 1206
  EnableWindow $1 1
  GetDlgItem $1 $HWND 1207
  EnableWindow $1 1
  Abort
validate:
  StrCpy $POP3_SERVER ""
  StrCpy $USERNAME ""
  StrCpy $PASSWORD ""
  ReadIniStr $0 "$PLUGINSDIR\auth.ini" "Field 1" "State"
  StrCmp $0 "1" done
  ReadINIStr $0 "$PLUGINSDIR\auth.ini" "Field 2" "State"
  StrCmp $0 "1" 0 validatePop3
    ReadINIStr $USERNAME "$PLUGINSDIR\auth.ini" "Field 3" "State"
    ReadINIStr $PASSWORD "$PLUGINSDIR\auth.ini" "Field 4" "State"
    StrCmp $USERNAME "" 0 +3
      MessageBox MB_ICONEXCLAMATION|MB_OK "You must enter a Username."
      Abort
    StrCmp $PASSWORD "" 0 +3
      MessageBox MB_ICONEXCLAMATION|MB_OK "You must enter a Password"
      Abort
    Goto done
validatePop3:
    ReadINIStr $POP3_SERVER "$PLUGINSDIR\auth.ini" "Field 6" "State"
    ReadINIStr $USERNAME "$PLUGINSDIR\auth.ini" "Field 7" "State"
    ReadINIStr $PASSWORD "$PLUGINSDIR\auth.ini" "Field 8" "State"
    StrCmp $POP3_SERVER "" 0 +3
      MessageBox MB_ICONEXCLAMATION|MB_OK "You must enter a POP3 Server."
      Abort
    StrCmp $USERNAME "" 0 +3
      MessageBox MB_ICONEXCLAMATION|MB_OK "You must enter a Username."
      Abort
    StrCmp $PASSWORD "" 0 +3
      MessageBox MB_ICONEXCLAMATION|MB_OK "You must enter a Password"
      Abort
done:
FunctionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\sendmail.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\sendmail.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  Delete "$INSTDIR\uninst.exe"
  Delete "$INSTDIR\sendmail.dpr"
  Delete "$INSTDIR\license.txt"
  Delete "$INSTDIR\ReadMe.html"
  Delete "$INSTDIR\sendmail.ini"
  Delete "$INSTDIR\sendmail.exe"

  Delete "$SMPROGRAMS\Sendmail\Uninstall.lnk"

  RMDir "$SMPROGRAMS\Sendmail"
  RMDir "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  SetAutoClose true
SectionEnd
